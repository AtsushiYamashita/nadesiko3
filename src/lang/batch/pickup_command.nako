# --- 基本パスを取得 ---
母艦パス取得してパス抽出して基本パスに代入。
プラグイン一覧は[
    'plugin_system.js',
    'plugin_browser.js',
    'plugin_turtle.js',
    'plugin_node.js'
 ]

 ●プラグイン要約作成(Fを)
 　　Ｆからファイル名抽出してプラグイン名に代入。
 　　「{改行}■{プラグイン名}」を表示。
 　　Ｆを開く
 　　改行で区切る
 　　ここから反復
 　　    トリムしてＳに代入。
 　　　　Ｓを「/^\/\/\/\s*(.+)\s*$/」で正規表現マッチ
 　　　　もし、そうならば
 　　　　　　見出し＝抽出文字列[0]
 　　　　　　「●{見出し}」を表示
 　　　　　　続ける。
 　　　　ここまで
 　　　　Ｓを「/"(.+?)".+const.+value:\s*(.+)\}/」で正規表現マッチ
 　　　　もし、そうならば
 　　　　　　定数定義＝抽出文字列[0]
 　　　　　　定数値＝抽出文字列[1]
 　　　　　　「| {定数定義} | 定数 | | {定数値}」を表示
 　　　　　　続ける。
 　　　　ここまで。
 　　　　Ｓを「/"(.+?)"\s*:\s*\{\s*\/\/\/\s*(.+)$/」で正規表現マッチ
 　　　　もし、そうならば
 　　　　　　関数定義＝抽出文字列[0]
 　　　　　　関数説明＝抽出文字列[1]
 　　　　　　続ける。
 　　　　ここまで
 　　　　もし、関数定義≠空ならば
 　　　　　　Ｓを「/josi\s*\:\s*(.+),\s*$/」で正規表現マッチ
 　　　　　　もし、そうならば
 　　　　　　　　それ＝抽出文字列[0]
 　　　　　　　　トリム
 　　　　　　　　「/^\[(.+)\]$/」を「$1」に正規表現置換
 　　　　　　　　「"」を空に置換
 　　　　　　　　助詞定義に代入。
 　　　　　　　　「| {関数定義} | 関数 | {助詞定義} | {関数説明}」を表示
 　　　　　　　　関数定義＝「」
 　　　　　　　　続ける。
 　　　　　　ここまで
 　　　　　　Ｓを「"func"」で正規表現マッチ。
 　　　　　　もし、そうならば、続ける。
 　　　　ここまで
 　　　　関数定義＝「」
 　　　　定数定義＝「」
 　　ここまで。
 ここまで。


プラグイン一覧を反復
　　パスは「./src/{それ}」
　　パスをプラグイン要約作成
　　表示ログを「{基本パス}/batch/command.txt」に保存。
ここまで
